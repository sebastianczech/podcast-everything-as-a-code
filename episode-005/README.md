# Odcinek 5 - Testowanie infrastruktury, piramida testów

Materiały:
  * [HashiCorp - Testing HashiCorp Terraform](https://www.hashicorp.com/blog/testing-hashicorp-terraform)
  * [HashiCorp - Test-Driven Development (TDD) for Infrastructure](https://www.hashicorp.com/resources/test-driven-development-tdd-for-infrastructure)
  * [HashiCorp - Testing Infrastructure as Code on Localhost](https://www.hashicorp.com/resources/testing-infrastructure-as-code-on-localhost)
  * [Design by Contract in Terraform](https://betterprogramming.pub/design-by-contracts-in-terraform-63467a749c1a)
  * [TDD vs TLD and what is the minimum code coverage needed](https://medium.com/swlh/tdd-vs-tld-and-what-is-the-minimum-code-coverage-needed-f380181d3400)
  * [DevOpsDays Warsaw 2022 - Sebastian Czech - Build infrastructure as a code (IaC) using test-later development (TLD) method](https://youtu.be/XY5LD2zy0eY?si=5BFggg3qJIpNGRln)
  * [HashiCorp - Tests - testing framework is available in Terraform v1.6.0 and later](https://developer.hashicorp.com/terraform/language/tests)
  * [HashiCorp - Checks - validate your infrastructure outside the usual resource lifecycle](https://developer.hashicorp.com/terraform/language/checks)
  * [HashiCorp - Mocks - mocking is available in Terraform v1.7.0 and later](https://developer.hashicorp.com/terraform/language/tests/mocking)
  * [HashiCorp - Terraform 1.7 adds test mocking and config-driven remove](https://www.hashicorp.com/blog/terraform-1-7-adds-test-mocking-and-config-driven-remove)
  * [OpenTofu - Command: test](https://opentofu.org/docs/cli/commands/test/)
  * [OpenTofu - What We Learned While Working on OpenTofu's New Test Feature](https://opentofu.org/blog/what-we-learned-while-working-on-opentofus-new-test-feature/)
  * [Spacelift - OpenTofu Tutorial – Getting Started, How to Install & Examples](https://spacelift.io/blog/opentofu-tutorial)
  * [Spacelift - Testing your Configuration with OpenTofu](https://www.youtube.com/watch?v=XjCS3vKLpkw&ab_channel=Spacelift)
  * [A Comprehensive Guide to Testing in Terraform: Keep your tests, validations, checks, and policies in order](https://medium.com/@mattiasfjellstrom/a-comprehensive-guide-to-testing-in-terraform-keep-your-tests-validations-checks-and-policies-038056da0a59)

Kurs:
  * [Test IaC on AWS](https://github.com/sebastianczech/aws-terratest-course)

Przykłady:

1. Przygotowanie kodu infrastruktury:

```bash
cd terraform-aws
terraform init
```

2. Testy jednostkowe:
  
[Walidacja kodu możliwa jest za pomocą komendy](https://developer.hashicorp.com/terraform/cli/commands/validate):

```bash
terraform validate
```

3. Testy kontraktowe:

[Walidacja zmiennych](https://www.terraform.io/language/values/variables) oraz [walidacja zasobów](https://www.terraform.io/language/expressions/custom-conditions) jest realizowana w trakcie wykonywania planu:

```bash
terraform plan
```

[W kodzie przygotowany został przykład walidacji zmiennych](terraform-aws/variables.tf):

```hcl
# example of contract tests using variables validation
variable "region" {
  description = "AWS region"
  type        = string

  # list of regions in AWS was generated by command:
  # for r in $(aws ec2 describe-regions --query 'Regions[*].RegionName' --output text); do echo $r; done
  validation {
    condition     = contains(["ap-northeast-1", "ap-northeast-2", "ap-northeast-3", "ap-south-1", "ap-southeast-1", "ap-southeast-2", "ca-central-1", "eu-central-1", "eu-north-1", "eu-west-1", "eu-west-2", "eu-west-3", "sa-east-1", "us-east-1", "us-east-2", "us-west-1", "us-west-2"], var.region)
    error_message = "The AWS region name is invalid."
  }
}
```

[Przygotowany został również przykład walidacji zasobów](terraform-aws/main.tf):

```hcl
resource "aws_lambda_function" "lambda_tf_demo" {
  filename         = "files/tf_demo.zip"
  function_name    = local.function_name_tf_demo
  role             = aws_iam_role.lambda_tf_demo_role.arn
  source_code_hash = filebase64sha256("files/tf_demo.zip")

  runtime = var.runtime
  handler = "tf_demo.lambda_handler"
  timeout = 10

  environment {
    variables = {
      foo = "bar"
    }
  }

  lifecycle {
    precondition {
      condition     = startswith(var.runtime, "python")
      error_message = "Python runtime is required to deploy Lambda"
    }
  }
}
```

4. Testy integracyjne:

Od [Terraform w wersji 1.7 można stosować tzw. mocki](https://developer.hashicorp.com/terraform/language/tests/mocking), których przykład znajdziesz w pliku [test_mock.tftest.hcl](terraform-aws/test_mock.tftest.hcl):

```bash
terraform test -filter=test_mock.tftest.hcl
```

5. Test funkcjonalne (E2E):

Od [Terraform w wersji 1.6 można pisać testy w HCL i je wywoływać za pomocą komendy "test"](https://developer.hashicorp.com/terraform/language/tests), których przykład znajdziesz w pliku [test_e2e.tftest.hcl](terraform-aws/test_e2e.tftest.hcl):

```bash
terraform test -filter=test_e2e.tftest.hcl
```

Ponadto możliwe jest stosowanie nowego bloku [check](https://developer.hashicorp.com/terraform/language/checks), które można wykorzystać do sprawdzania wdrożonych zasobów:

```hcl
# example of e2e test using check block, which validates infrastructure after Terraform applies changes
check "lambda_deployed" {
  data "http" "terraform_io" {
    url = aws_lambda_function_url.lambda_tf_demo_endpoint.function_url
  }

  assert {
    # If we execution function using URL without authentication, then it should be received forbidden message, if Lambda is deployed correctly
    condition = data.http.terraform_io.status_code == 403
    error_message = format("The Lambda %s is not deployed.",
      aws_lambda_function.lambda_tf_demo.function_name
    )
  }
}
```
